plugins {
    id 'java'
}

configurations {
    test
    junit4TestImplementation {
        extendsFrom testImplementation
    }
    junit5TestImplementation {
        extendsFrom testImplementation
    }
}


sourceSets {

    junit5Test {
        java {
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
            compileClasspath += sourceSets.test.output
            runtimeClasspath += sourceSets.test.output
        }
        resources {
            srcDirs = ['src/test/resources', 'src/resources', 'src/junit5Test/resources']
        }
    }

    junit4Test {
        java {
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
            compileClasspath += sourceSets.test.output
            runtimeClasspath += sourceSets.test.output
        }
        resources {
            srcDirs = ['src/test/resources', 'src/resources']
        }
    }
}

task junit5Test(type: Test) {
    group = 'verification'
    testClassesDirs = sourceSets.junit5Test.output.classesDirs
    classpath = sourceSets.junit5Test.runtimeClasspath
    shouldRunAfter test
}

task junit4Test(type: Test) {
    group = 'verification'
    testClassesDirs = sourceSets.junit4Test.output.classesDirs
    classpath = sourceSets.junit4Test.runtimeClasspath
    shouldRunAfter test
}

dependencies {

    implementation(group: 'org.hibernate', name: 'hibernate-core', version: '5.3.7.Final')
    implementation(group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final')
    implementation(group: 'org.hibernate', name: 'hibernate-envers', version: '5.3.7.Final')
    implementation("javax.inject:javax.inject:1")

    testImplementation(group: 'org.assertj', name: 'assertj-core', version: '3.20.2')
    testImplementation group: 'com.h2database', name: 'h2', version: '1.4.200'
    testImplementation(group: 'org.hsqldb', name: 'hsqldb', version: '2.5.0')
    testImplementation(group: 'org.hibernate', name: 'hibernate-validator', version: '6.0.14.Final')
    testImplementation(group: 'org.hibernate', name: 'hibernate-validator-cdi', version: '6.0.14.Final')
    testImplementation(group: 'org.glassfish', name: 'javax.el-impl', version: '3.0.1-b08-jbossorg-1')


    junit4TestImplementation 'junit:junit:4.12'
    junit4TestImplementation(group: 'com.github.database-rider', name: 'rider-core', version: '1.31.1-SNAPSHOT')

    // junit5
    junit5TestImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    junit5TestImplementation 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
    junit5TestImplementation(group: 'org.jboss.weld', name: 'weld-junit5', version: '2.0.2.Final')
    junit5TestImplementation(group: 'com.github.database-rider', name: 'rider-junit5', version: '1.31.1-SNAPSHOT')

    // JTA
    implementation(group: 'javax.enterprise', name: 'cdi-api', version: '2.0.SP1')
    implementation(group: 'org.jboss.narayana.jta', name: 'narayana-jta', version: '5.9.0.Final')
    implementation(group: 'org.jboss.spec.javax.transaction', name: 'jboss-transaction-api_1.2_spec', version: '1.0.1.Final')
    implementation(group: 'org.jboss.naming', name: 'jnpserver', version: '5.0.5.Final')
    implementation(group: 'org.jboss.weld.module', name: 'weld-jta', version: "3.0.5.Final")


}

junit5Test {
    useJUnitPlatform()
}

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}
